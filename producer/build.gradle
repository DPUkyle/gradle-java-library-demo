plugins {    
    id 'java-library'
    id 'maven-publish'
    id 'ivy-publish'
}

group = "com.sample.producer"
version = "1.2"

// do not publish .module files for now
tasks.withType(GenerateModuleMetadata) {
  enabled = false
}

repositories {
    jcenter()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.guava:guava:27.0.1-jre'
}

publishing {
    publications {
        maven(MavenPublication) {            
            from components.java
        }
        ivy(IvyPublication) {
            from components.java
            // put "primary" artifact on the master configuration
            configurations.create 'master'
            descriptor {
                withXml {
                    def root = it.asNode()
                    def publications = root.publications ? root.publications[0] : root.appendNode('publications')
                    // hack - sets the sole artifact's conf attribute to be "master" (was: "compile, runtime")
                    publications.artifact[0].@conf = 'master'
                }
            }
        }
    }
    repositories {
        ivy {
            url = System.getProperty("user.home") + "/local-ivy-repo"
        }
    }
}